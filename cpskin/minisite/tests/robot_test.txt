*** Settings ***

Resource  plone/app/robotframework/selenium.robot
Library  Remote  ${PLONE_URL}/RobotRemote

Suite Setup  Start browser
Suite Teardown  Close All Browsers

*** Variables ***

${BROWSER} =  firefox
${SUBDOMAIN_PLONE_URL} =   http://127.0.0.1:${ZOPE_PORT}/${PLONE_SITE_ID}

*** Test Cases ***

Plone site does not show minisite viewlet
    Go to  ${PLONE_URL}
    Page should contain  Plone site
    Page should not contain  This is a minisite in portal mode.
    Page should contain element  css=div.searchSection
    Element should be visible  css=div.searchSection

Plone control panel does show panel link
    Enable autologin as  Manager
    Go to  ${PLONE_URL}/plone_control_panel
    #Import library  DebugLibrary
    #Debug
    Page should contain  Mini sites
    Page should contain link  @@minisites_panel

Control panel does show panel minisites
    Enable autologin as  Manager
    ${folder}=  Create content  id=folder  type=Folder  title=Folder
    ${minisite}=  Create content  id=minisite  type=Folder  title=Mini site  container=${folder}
    Go to  ${PLONE_URL}/plone_control_panel
    Page should contain link  @@minisites_panel
    Click link  @@minisites_panel
    Page should contain  tests/minisites_config.txt
    Page should contain  Path /plone/folder/minisite
    Page should contain  Minisite does not exist


Minisite does show minisite viewlet in portal mode
    Enable autologin as  Manager
    Create content  id=minisite  type=Folder  title=Mini site
    Go to  ${PLONE_URL}/minisite
    Page should contain  Mini site
    Page should contain  This is a minisite in portal mode.
    Page should contain element  css=div.searchSection
    Element should be visible  css=div.searchSection

Minisite does show minisite viewlet in minisite mode
    Enable autologin as  Manager
    Create content  id=minisite  type=Folder  title=Mini site
    Go to  ${SUBDOMAIN_PLONE_URL}/minisite
    Page should contain  Mini site
    Page should contain  This is a minisite in minisite mode.
    Page should contain element  css=div.searchSection
    Element should not be visible  css=div.searchSection

Subfolder of minisite does show minisite viewlet
    Enable autologin as  Manager
    ${minisite}=  Create content  id=minisite  type=Folder  title=Mini site
    Create content  id=subfolder  type=Folder  title=Subfolder  container=${minisite}
    Go to  ${PLONE_URL}/minisite/subfolder
    Page should contain  Subfolder
    Page should contain  This is a minisite in portal mode.

Minisite as subfolder does show minisite viewlet
    Enable autologin as  Manager
    ${folder}=  Create content  id=folder  type=Folder  title=Folder
    Create content  id=minisite  type=Folder  title=Mini site  container=${folder}
    Go to  ${PLONE_URL}/folder/minisite
    Page should contain  Mini site
    Page should contain  This is a minisite in portal mode.

Search with searchbox in minisite as subfolder does show all content
    Enable autologin as  Manager
    ${folder}=  Create content  id=folder  type=Folder  title=Folder
    Create content  id=to_find  type=Folder  title=To find in folder  container=${folder}
    ${minisite}=  Create content  id=minisite  type=Folder  title=Mini site  container=${folder}
    Create content  id=to_find  type=Folder  title=To find in minisite  container=${minisite}
    Go to  ${PLONE_URL}/folder/minisite
    Page should contain  Mini site
    Page should contain  This is a minisite in portal mode.
    Input text  css=#searchGadget  To find
    Page should contain  To find in minisite
    Page should contain  To find in folder
    Click element  css=#portal-searchbox .searchButton
    Wait Until Page contains  To find in minisite
    Wait Until Page contains  To find in folder
    Page should contain  To find in minisite
    Page should contain  To find in folder

Minisite mode Search with searchbox in minisite as subfolder does show all content
    [Tags]  rob_current 
    Enable autologin as  Manager
    ${folder}=  Create content  id=folder  type=Folder  title=Folder
    Create content  id=to_find  type=Folder  title=To find in folder  container=${folder}
    ${minisite}=  Create content  id=minisite  type=Folder  title=Mini site  container=${folder}
    Create content  id=to_find  type=Folder  title=To find in minisite  container=${minisite}
    Go to  ${SUBDOMAIN_PLONE_URL}/folder/minisite
    Page should contain  Mini site
    Page should contain  This is a minisite in minisite mode.
    Input text  css=#searchGadget  To find
    Page should contain  To find in minisite
    Page should not contain  To find in folder
    Click element  css=#portal-searchbox .searchButton
    Location Should Contain  minisite/@@search
    Page should contain  To find in minisite
    Page should not contain  To find in folder
    Click button  css=#search-field .searchButton
    Location Should Contain  minisite/@@search
    Page should contain  To find in minisite
    Page should not contain  To find in folder

*** Keywords ***

Start browser
    Open browser  http://localhost:55001/plone/  browser=${BROWSER}
